module home
import rightsidebar
import leftsidebar

site
 index.html: main
end

def css(url, media)
  link(rel="stylesheet", href=url, type="text/css", media=media);
end

def preface(year,doi)
  a(href="http://dx.doi.org/10.1016/" + doi) year;
end

//def sp 
//  echo "&nbsp;";
//end


def content
  ldtaHeading();
  updates();
end


def ldtaHeading
 p "International Workshop on Language Descriptions, Tools, 
    and Applications" ;
end

def updates
section("From LDTA to SLE") {
 echo "February 28th, 2013";
 br;
 br;
 echo "The LDTA workshop series has been discontinued. The Steering Committee thanks all
  authors, program committee members and organisers for their work on LDTA over the
  years.";
 br;
 br;
 echo "Authors of papers in the LDTA topic area are strongly encouraged to submit their work to the "; 
 a(href="http://planet-sl.org/sleconf") "International Conference on Software Language Engineering"; 
 echo " instead.";
}
end

def parTitle(title)
 h1.title title;
end

def par 
 div.entry {
  yield;
 }
end

def section(title) 
  parTitle(title);
  par yield;
end

def subtitle(title)
  p.meta small title;
end

def main 
html(xmlns='http://www.w3.org/1999/xhtml) {
 
    head {
     title "LDTA";
     meta(http-equiv="content-type", content="text/html; charset=utf-8");
     meta(name="keywords", content="Programming Debugging Language Descriptions Parsing Parsers Term Rewriting Attribute Grammars Functional Programming Debugging Model Driven Engineering Formal Methods Meta Programming");
     meta(name="description", content="The International Workshop on Language Descriptions Tools and Applications");
     meta(name="language", content="NL");
     css("./style.css", "screen");
     comment "Design by Free CSS Templates\nhttp://www.freecsstemplates.org\nReleased for free under a Creative Commons Attribution 2.5 License\n\nName       : Compressed \nDescription: A three-column, fixed-width template fit for 1024x768 screen resolutions.\nVersion    : 1.0\nReleased   : 20080524";
    }

    body {
      div#logo h1.logo "Workshop on Language Descriptions, Tools, and Applications";
      div#page {
        div.sidebar#sidebar1 {
         br; 
         previous;
        }

      div#content div.bgtop div.bgbtm div.post content();

    div.sidebar#sidebar2 {
      br;
      relatedLinks;
    }
   }
    div(style="clear: both;") {
      br;
      div#footer p.legal "&copy;2007 All Rights Reserved. 
      Design by <a(href="http://www.freecsstemplates.org/") "Free CSS Templates">";
    }
  }
  }
end
